plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '1.5.0'
description = project.name

compileJava.options.encoding = 'UTF-8'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        url 'https://jitpack.io'
        name 'JitPack'
    }

    maven {
        url 'https://repo.viaversion.com'
        name 'ViaVersion'
    }

    maven {
        url 'https://raw.github.com/ThomasOM/Pledge/repository'
        name 'Pledge'
    }
}

dependencies {
    // Spigot 1.8.8 - necessary for project; provided in libs
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot', version: '1.8.8-R0.1-SNAPSHOT'

    // JOML - for vector math
    // https://mvnrepository.com/artifact/org.joml/joml
    implementation group: 'org.joml', name: 'joml', version: '1.10.5'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '24.1.0'

    // ViaVersion - for version checking in PacketEvents
    // https://github.com/ViaVersion/ViaVersion
    compileOnly group: 'com.viaversion', name: 'viaversion-api', version: '4.10.1'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.32'
}

tasks {
    jar {
        archiveFileName.set("${project.name}-${project.version}-default.jar")
    }

    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        enableRelocation = true
        relocationPrefix = "net.foulest.vulture.shaded"
        minimize()
    }

    compileJava {
        dependsOn(clean)
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/*.yml') {
            filter { line ->
                line.replace('${project.version}', project.version)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "net.foulest.vulture"
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
