plugins {
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '2.0.3'
description = project.name

// Set the project's language level
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        name 'SpigotMC'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name 'Sonatype Snapshots'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'Sonatype Releases'
        url 'https://oss.sonatype.org/content/repositories/central'
    }

    maven {
        name 'CodeMC Releases'
        url 'https://repo.codemc.io/repository/maven-releases/'
    }

    maven {
        name 'CodeMC Snapshots'
        url 'https://repo.codemc.io/repository/maven-snapshots/'
    }
}

dependencies {
    // Spigot 1.8.8 API - necessary for project
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.8.8-R0.1-SNAPSHOT'

    // Netty - for networking
    // https://mvnrepository.com/artifact/io.netty/netty-all
    compileOnly group: 'io.netty', name: 'netty-all', version: '4.1.118.Final'

    // PacketEvents - for packet manipulation
    // https://packetevents.com
    compileOnly group: 'com.github.retrooper', name: 'packetevents-spigot', version: '2.7.0'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.2'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
}

tasks {
    jar {
        archiveFileName.set("${project.name}-${project.version}-default.jar")
    }

    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        enableRelocation = true
        relocationPrefix = 'net.foulest.vulture.shaded'
    }

    compileJava {
        dependsOn(clean)
        options.encoding = 'UTF-8'
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/*.yml') {
            filter { final line ->
                line.replace('${project.version}', project.version)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.foulest.vulture'
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
