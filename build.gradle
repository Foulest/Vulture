plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '2.0.2'
description = project.name

// Set the project's language level
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        name 'SpigotMC'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        // As of Gradle 5.1, you can limit this to only those
        // dependencies you expect from it
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven {
        name 'Sonatype Snapshots'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'Sonatype Releases'
        url 'https://oss.sonatype.org/content/repositories/central'
    }

    maven {
        name 'JitPack'
        url 'https://jitpack.io'
    }

    maven {
        name 'ViaVersion'
        url 'https://repo.viaversion.com'
    }

    maven {
        name 'Pledge'
        url 'https://raw.github.com/ThomasOM/Pledge/repository'
    }

    maven {
        name 'Minebench'
        url 'https://repo.minebench.de/'
    }
}

dependencies {
    // Spigot 1.8.8 API - necessary for project
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version: '1.21.4-R0.1-SNAPSHOT'

    // Netty - for networking
    // https://mvnrepository.com/artifact/io.netty/netty-all
    compileOnly group: 'io.netty', name: 'netty-all', version: '4.1.116.Final'

    // Authlib - for Mojang authentication
    // Latest working version: 2.0.27 from Minebench
    // https://mvnrepository.com/artifact/com.mojang/authlib
    compileOnly group: 'com.mojang', name: 'authlib', version: '2.0.27'

    // ViaVersion - for version checking in PacketEvents
    // Note: Keep on version 4.10.2 for compatibility with PacketEvents
    // https://github.com/ViaVersion/ViaVersion
    compileOnly group: 'com.viaversion', name: 'viaversion-api', version: '4.10.2'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.1'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
}

tasks {
    jar {
        archiveFileName.set("${project.name}-${project.version}-default.jar")
    }

    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        enableRelocation = true
        relocationPrefix = 'net.foulest.vulture.shaded'
        minimize()
    }

    compileJava {
        dependsOn(clean)
        options.encoding = 'UTF-8'
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/*.yml') {
            filter { final line ->
                line.replace('${project.version}', project.version)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.foulest.vulture'
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
